grammar nl.dslmeinte.xtext.conditional.Conditional hidden(WHITESPACE)

import "http://www.eclipse.org/emf/2002/Ecore" as ecore

generate conditional "http://www.dslmeinte.nl/xtext/conditional/Conditional"

/*
 * A very simple DSL for conditional logic/expressions,
 * to be composed by other languages inside plain strings.
 */

Condition:
	expr=ConditionalExpression;

ConditionalExpression: ConditionalOR;

ConditionalOR returns ConditionalExpression:
    ConditionalAND ({ConditionalOR.left=current} '||' right=ConditionalAND)*;

ConditionalAND returns ConditionalExpression:
    ConditionalNOT ({ConditionalAND.left=current} '&&' right=ConditionalNOT)*;

ConditionalNOT returns ConditionalExpression:
    ConditionalPrimary | ( {ConditionalNOT} '!' expr=ConditionalPrimary );

ConditionalPrimary returns ConditionalExpression:
      ( '(' ConditionalOR ')' )
    | ( {ConditionLiteral} id=IDENTIFIER )
    ;


terminal IDENTIFIER	: ('a'..'z'|'A'..'Z'|'_'|'0'..'9')*;

terminal WHITESPACE	: (' '|'\t'|'\r'|'\n')+;

