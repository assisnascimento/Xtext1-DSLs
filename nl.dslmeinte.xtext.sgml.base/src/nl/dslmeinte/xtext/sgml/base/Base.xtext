grammar nl.dslmeinte.xtext.sgml.base.Base

generate base "http://www.dslmeinte.nl/xtext/sgml/dtd/Base"

import "http://www.eclipse.org/emf/2002/Ecore" as ecore
	// (need to import Ecore because of terminal definitions)


// will be overridden by including grammar to add 'root' feature:
SgmlDocument hidden(WHITESPACE, HEADER_COMMENTS):
	'<' '!DOCTYPE' rootName=IDENTIFIER ('PUBLIC' | 'SYSTEM') header1=QUOTED_STRING header2=QUOTED_STRING?
		('<' entities+=Entity* ']')? '>'
	;

Entity hidden(WHITESPACE, HEADER_COMMENTS):
	'<' '!ENTITY' name=IDENTIFIER 'SYSTEM'? path=QUOTED_STRING '>'
	;

_PCDATA:
	contents+=_PCDATAContents+;

_PCDATAContents:
	_PCDATALiteralContents | _PCDATAEntityReference;

_PCDATALiteralContents:
	text=(LITERAL_CONTENTS|WHITESPACE);

_PCDATAEntityReference:
	'&' entity=[Entity|IDENTIFIER] ';';


terminal IDENTIFIER				: ('a'..'z'|'A'..'Z'|'_') ('a'..'z'|'A'..'Z'|'_'|'0'..'9')*;
terminal QUOTED_STRING			:
									'"' ( '\\' ('b'|'t'|'n'|'f'|'r'|'"'|"'"|'\\') | !('\\'|'"') )* '"' |
									"'" ( '\\' ('b'|'t'|'n'|'f'|'r'|'"'|"'"|'\\') | !('\\'|"'") )* "'"
									;

terminal SGML_COMMENTS			: '<!--' -> '-->';
terminal HEADER_COMMENTS		: '--' -> '--';

terminal WHITESPACE				: (' '|'\t'|'\r'|'\n')+;

terminal LITERAL_CONTENTS		: '#something';		// ...that doesn't match with any other terminal here so ANTLR doesn't get confused for nothing because we\'re rolling our own lexer anyway...

