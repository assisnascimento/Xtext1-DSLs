grammar nl.dslmeinte.xtext.sgml.dtd.Base

generate base "http://www.dslmeinte.nl/xtext/sgml/dtd/Base"

import "http://www.eclipse.org/emf/2002/Ecore" as ecore
	// (need to import Ecore because of terminal definitions)

// will be overridden by including grammar to add 'root' feature:
SgmlDocument hidden(WHITESPACE, HEADER_COMMENTS):
	OPEN_TAG 'SISGML' ('PUBLIC'|'SYSTEM') header1=QUOTED_STRING header2=QUOTED_STRING?
		(OPEN_BRACKET entities+=Entity* CLOSE_BRACKET)? CLOSE_TAG
	;

Entity hidden(WHITESPACE, HEADER_COMMENTS):
	OPEN_TAG ENTITY_KEYWORD name=ID 'SYSTEM'? CLOSE_TAG
	;

// FIXME  #PCDATA not assigned to anything yet (also get rid of *-card. in generated grammar)...

_PCDATA:
	_PCDATALiteralContents | _PCDATAEntityReference;

_PCDATALiteralContents:
	text=LITERAL_CONTENTS;

_PCDATAEntityReference:
	AMPERSAND entity=[Entity] SEMICOLON;


terminal ID  				: ('a'..'z'|'A'..'Z'|'_') ('a'..'z'|'A'..'Z'|'_'|'0'..'9')*;
terminal QUOTED_STRING		:
								'"' ( '\\' ('b'|'t'|'n'|'f'|'r'|'"'|"'"|'\\') | !('\\'|'"') )* '"' |
								"'" ( '\\' ('b'|'t'|'n'|'f'|'r'|'"'|"'"|'\\') | !('\\'|"'") )* "'"
								;

terminal SGML_COMMENTS		: '<!--' -> '-->';
terminal HEADER_COMMENTS	: '--' -> '--';

terminal WHITESPACE			: (' '|'\t'|'\r'|'\n')+;

/*
 * Note that we give the keywords which are known beforehand
 * their own, separate terminal rules so that it is easier
 * to marry up the generic SgmlLexer part to the custom
 * Lexer.
 */

terminal OPEN_TAG			: '<';
terminal CLOSE_TAG			: '>';
terminal SLASH_SYMBOL		: '/';

terminal DOCTYPE_KEYWORD	: '!DOCTYPE';
terminal ENTITY_KEYWORD		: '!ENTITY';

terminal OPEN_BRACKET		: '[';
terminal CLOSE_BRACKET		: ']';

terminal AMPERSAND			: '&';
terminal SEMICOLON			: ';';

terminal LITERAL_CONTENTS	: .;

